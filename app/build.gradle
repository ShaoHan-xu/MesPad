apply plugin: 'com.android.application'

def keystoreFilepath = ""
def keystorePSW = ""
def keystoreAlias = ""
def keystoreAliasPSW = ""
// default keystore file, PLZ config file path in local.properties
def keyFile = file('s.keystore.temp')

Properties properties = new Properties()
// local.properties file in the root director
properties.load(project.rootProject.file('local.properties').newDataInputStream())
keystoreFilepath = properties.getProperty("keystore.path")

if (keystoreFilepath) {
    keystorePSW = properties.getProperty("keystore.password")
    keystoreAlias = properties.getProperty("keystore.alias")
    keystoreAliasPSW = properties.getProperty("keystore.alias_password")
    keyFile = file(keystoreFilepath)
}
android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.eeka.mespad"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 28
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }

    signingConfigs {
        debug {
            v1SigningEnabled true
            v2SigningEnabled true

            keyAlias keystoreAlias
            keyPassword keystoreAliasPSW
            storeFile keyFile
            storePassword keystorePSW
        }
        release {
            v1SigningEnabled true
            v2SigningEnabled true

            keyAlias keystoreAlias
            keyPassword keystoreAliasPSW
            storeFile keyFile
            storePassword keystorePSW
        }
    }

    buildTypes {
        release {
            //混淆
            minifyEnabled true
            //移除无用资源
            shrinkResources true
            //zipAlign优化
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
        }
    }

    productFlavors {
        yudu {
//            applicationId "com.eeka.mespad.yd"
            resValue "string", "app_name", "赢领智裳_YD"
        }
        longhua {
//            applicationId "com.eeka.mespad.lh"
            // 动态添加 string.xml 字段；
            // 注意，这里是添加，在 string.xml 不能有这个字段，会重名！！！
            resValue "string", "app_name", "赢领智裳_LH"
        }

    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [CHANNEL_VALUE: name]
    }

    //指定打包后应用名称
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            def fileName
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                if (variant.buildType.name == 'release') {
                    fileName = "MesPad_${defaultConfig.versionName}_${defaultConfig.versionCode}_${variant.productFlavors[0].name}.apk"
//                    fileName = "MesPad.apk"
                } else if (variant.buildType.name == 'debug') {
                    fileName = "MesPad_${defaultConfig.versionName}_${defaultConfig.versionCode}_${variant.productFlavors[0].name}_debug.apk"
                }
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }
}
repositories {
    maven {
        url "https://repo.eclipse.org/content/repositories/paho-releases/"
    }
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile project(':OkHttpFinal')
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.bm.photoview:library:1.4.1'
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:recyclerview-v7:25.2.0'
    compile 'com.danikula:videocache:2.7.0'
    compile 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.1.0'
    compile 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.0'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.google.zxing:core:3.3.0'
    compile files('libs/guavalib.jar')
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.tencent.bugly:crashreport_upgrade:1.3.1'
}
