apply plugin: 'com.android.application'

def keystoreFilepath = ""
def keystorePSW = ""
def keystoreAlias = ""
def keystoreAliasPSW = ""
// default keystore file, PLZ config file path in local.properties
def keyFile = file('s.keystore.temp')

Properties properties = new Properties()
// local.properties file in the root director
properties.load(project.rootProject.file('local.properties').newDataInputStream())
keystoreFilepath = properties.getProperty("keystore.path")

if (keystoreFilepath) {
    keystorePSW = properties.getProperty("keystore.password")
    keystoreAlias = properties.getProperty("keystore.alias")
    keystoreAliasPSW = properties.getProperty("keystore.alias_password")
    keyFile = file(keystoreFilepath)
}
android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.eeka.mespad"
        minSdkVersion 15

        targetSdkVersion 28
        versionCode 135

        versionName "3.135"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }

    signingConfigs {
        debug {
            v1SigningEnabled true
            v2SigningEnabled true

            keyAlias keystoreAlias
            keyPassword keystoreAliasPSW
            storeFile keyFile
            storePassword keystorePSW
        }
        release {
            v1SigningEnabled true
            v2SigningEnabled true

            keyAlias keystoreAlias
            keyPassword keystoreAliasPSW
            storeFile keyFile
            storePassword keystorePSW
        }
    }

    buildTypes {
        release {
            //混淆
//            minifyEnabled true
            //移除无用资源
//            shrinkResources true
            //zipAlign优化
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
        }
    }

    productFlavors {
        YD {
            // 动态添加 string.xml 字段；
            // 注意，这里是添加，在 string.xml 不能有这个字段，会重名！！！
            resValue "string", "app_name", "赢领智尚_YD"
            resValue "string", "app_channel", "YD"
            resValue "string", "system_code", "P"
        }
        YD_BULK {
            resValue "string", "app_name", "赢领智尚_YD"
            resValue "string", "app_channel", "YD_BULK"
            resValue "string", "system_code", "P"
        }
        LH {
            resValue "string", "app_name", "赢领智尚_LH"
            resValue "string", "app_channel", "LH"
            resValue "string", "system_code", "Q"
        }
        LH_BULK {
            resValue "string", "app_name", "赢领智尚_LH"
            resValue "string", "app_channel", "LH_BULK"
            resValue "string", "system_code", "LH_P"
        }
        LH_P {
            resValue "string", "app_name", "赢领智尚_LH"
            resValue "string", "app_channel", "LH_P"
            resValue "string", "system_code", "LH_P"
        }
        BASE {
            resValue "string", "app_name", "赢领智尚"
            resValue "string", "app_channel", "BASE"
            resValue "string", "system_code", "D"
        }
    }

    flavorDimensions 'api'
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [CHANNEL_VALUE: name]
    }

    //指定打包后应用名称
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "MesPad_${variant.productFlavors[0].name}_${variant.versionName}.apk"
        }
    }

}
repositories {
    maven {
        url "https://repo.eclipse.org/content/repositories/paho-releases/"
    }
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation project(':OkHttpFinal')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.bm.photoview:library:1.4.1'
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.danikula:videocache:2.7.0'
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.0'
    implementation 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.0'
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.tencent.bugly:crashreport_upgrade:1.4.1'
    implementation 'com.google.zxing:core:3.3.0'
    implementation files('libs/guavalib.jar')
    implementation files('libs/ksoap2-android-assembly-3.3.0-jar-with-dependencies.jar')
    implementation files('libs/zp_cpcl_new.jar')
    implementation files('libs/jcifs-1.3.19.jar')
}
